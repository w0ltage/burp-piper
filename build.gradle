/*
 * This file is part of Piper for Burp Suite (https://github.com/silentsignal/burp-piper)
 * Copyright (c) 2018 Andras Veres-Szentkiralyi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
    id 'com.google.protobuf' version '0.9.4'
}

// NOTE: Java version upgraded from 1.8 to 17 for Montoya API compatibility
// The new Montoya API requires Java 17+, breaking backward compatibility with Java 8
sourceCompatibility = 17
targetCompatibility = 17

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileKotlin {
    kotlinOptions.jvmTarget = '17'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '17'
}


repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.24"
    implementation "com.google.protobuf:protobuf-lite:3.0.0"
    implementation "org.snakeyaml:snakeyaml-engine:1.0"

    implementation 'net.portswigger.burp.extensions:montoya-api:2025.5'
    implementation 'com.esotericsoftware:minlog:1.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-inline:3.12.4'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    // Include main resources in test classpath for resource loading
    classpath += sourceSets.main.runtimeClasspath
}

jar {
    from('src/main/yaml') {
        include('*.yaml')
    }
	from('static') {
		include('mime.pb')
	}
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.0.0' }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += file("${protobuf.generatedFilesBaseDir}/main/javalite")
        }
    }
}
